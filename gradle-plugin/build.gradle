apply plugin: 'groovy'
apply plugin: 'bintray'
apply plugin: 'idea'

targetCompatibility = JavaVersion.VERSION_1_6
sourceCompatibility = JavaVersion.VERSION_1_6

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile project(':processor')

  compileOnly "com.android.tools.build:gradle-api:$androidToolsVersion"
}

ext {
  generatedDir = new File(projectDir, "generated")
  generatedJavaSourcesDir = new File(generatedDir, "main/groovy")
  buildClassFile = new File(generatedJavaSourcesDir, "io/michaelrocks/paranoid/plugin/Build.groovy")
}

sourceSets {
  main {
    output.dir(generatedJavaSourcesDir, builtBy: 'generateBuildClass')
    groovy.srcDir generatedJavaSourcesDir
  }
}

task generateBuildClass {
  inputs.property('version', version)
  outputs.dir generatedDir

  doLast {
    buildClassFile.parentFile.mkdirs()
    buildClassFile.text = "" +
        "package io.michaelrocks.paranoid.plugin\n" +
        "\n" +
        "public class Build {\n" +
        "    public static final String VERSION = \"$version\"\n" +
        "}\n"
  }
}

idea {
  module {
    generatedSourceDirs += generatedJavaSourcesDir
  }
}
